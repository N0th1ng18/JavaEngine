#version 400

//MAX_LIGHTS = 20

uniform sampler2D modelTexture;
uniform sampler2D normalMap;
uniform float shineDamper;
uniform float reflectivity;
uniform float ambient;
uniform vec3 lightColor[20];
uniform float attenuationRadius[20];
uniform int numLights;
uniform int isLight;
uniform vec3 fogColor;

in vec2 texCoord_V;
in vec3 toLightVector[20];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Color;

void main() {

	if(isLight > 0){
	
	
		//*******************Light******************************
		vec3 totalDiffuse = vec3(0.0);
		vec3 totalSpecular = vec3(0.0);
		
		totalDiffuse = lightColor[isLight-1];
		totalDiffuse = max(totalDiffuse, ambient);
		out_Color = texture2D(modelTexture, texCoord_V) * vec4(totalDiffuse, 1.0) + vec4(totalDiffuse, 1.0);
		out_Color = mix(vec4(fogColor, 1.0), out_Color, visibility);
		//******************************************************
	
	
	} else {
	
	
		//********************NonLight***************************
		/*Normal Map*/
		vec4 normalMapValue = 2.0 * texture(normalMap, texCoord_V) - 1.0;
		
		vec3 unitNormal = normalize(normalMapValue.rgb);
		vec3 unitVectorToCamera = normalize(toCameraVector);
		
		vec3 totalDiffuse = vec3(0.0);
		vec3 totalSpecular = vec3(0.0);
			
		//Go through each light
		for(int i=0; i < numLights;i++){
		
			/*Distance*/
				float distance = length(toLightVector[i]);	//Distance from point to light source
				
				if(distance <= attenuationRadius[i]){
					/*Attenuation*/
						float attFactor = clamp(1.0 - distance*distance/(attenuationRadius[i]*attenuationRadius[i]), 0.0, 1.0);attFactor *= attFactor; //softer formula
						//float attFactor = clamp(1.0 - distance/attenuationRadius[i], 0.0, 1.0); attFactor *= attFactor; //sharper formula
					
					/*Diffuse Lighting*/
							vec3 unitLightVector = normalize(toLightVector[i]);
							float nDot1 = dot(unitNormal, unitLightVector);
							float brightness = max(nDot1, 0.0);
						totalDiffuse = totalDiffuse + ((brightness * lightColor[i])*attFactor);
				
					/*Specular Lighting*/
							vec3 lightDirection = -unitLightVector;
							vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);
							float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
							specularFactor = max(specularFactor, 0.0);
							float dampedFactor = pow(specularFactor, shineDamper);
						totalSpecular = totalSpecular + ((dampedFactor * reflectivity * lightColor[i])*attFactor);
				}
		}
		
		totalDiffuse = max(totalDiffuse, ambient);
	
		out_Color = texture2D(modelTexture, texCoord_V) * vec4(totalDiffuse, 1.0) + vec4(totalSpecular, 1.0);
		out_Color = mix(vec4(fogColor, 1.0), out_Color, visibility);
		//******************************************************
	}
	
}